apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// Load and read app.properties file
def appProps = new Properties()
appProps.load(project.rootProject.file("app.properties").newDataInputStream())

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "id.mustofa.app.amber"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Create field called MOVIEDB_API_KEY and store api key from appProps with tmdb.appid key
        buildConfigField 'String', 'MOVIEDB_API_KEY', "\"${appProps.getProperty('tmdb.apikey', '')}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    // default
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // android
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.paging:paging-runtime:2.1.0'

    // material
    implementation 'com.google.android.material:material:1.0.0'

    // glide
    def glide_version = "4.9.0"
    implementation("com.github.bumptech.glide:glide:$glide_version") { exclude group: "androidx" }
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // arch room
    def room_version = "2.1.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // arch lifecycle
    def lifecycle_version = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-alpha03"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // retrofit
    def retrofit_version = "2.6.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // arch testing
    def archTest_version = "2.0.1"
    testImplementation "androidx.arch.core:core-testing:$archTest_version"
    androidTestImplementation "androidx.arch.core:core-testing:$archTest_version"

    // junit
    def junit_version = "4.12"
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "junit:junit:$junit_version"

    // mockito
    def mockito_version = "2.23.4"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"

    // espresso
    def espresso_version = "3.2.0"
    implementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"

    // android test runner & rules
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test:core:1.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"

    // coroutines test
    def coroutines_version = "1.2.1"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
}
